# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Hussam Al-Tayeb ht990332@gmail.com
# Contributor: Khashayar Naderehvandi <khashayar [at] naderehvandi [dot] net>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>

pkgbase=compiz
pkgname=('compiz-core'
	'compiz-decorator-kde'
	'compiz-decorator-gtk')
pkgver=0.8.8
pkgrel=5
pkgdesc="Composite manager for Aiglx and Xgl"
url="http://www.compiz.org/"
license=('GPL' 'LGPL' 'MIT')
arch=('i686' 'x86_64')
groups=('compiz' 'compiz-kde' 'compiz-gtk' 'compiz-fusion')
makedepends=('intltool' 'gnome-control-center' 'gconf' 'libwnck' 'kdebase-workspace'
 'startup-notification' 'librsvg' 'libgl' 'dbus' 'glu' 'libxslt' 'fuse' 'metacity')
options=(!libtool !emptydirs)
conflicts=('compiz' 'compiz-core-git' 'compiz-git')
replaces=('compiz')
source=(http://releases.compiz.org/0.8.8/compiz-$pkgver.tar.gz
        compiz-kde-4.8.patch
        compiz-kde-4.9.patch
        compiz-kde-4.10.patch
        compiz-gcc-4.7.patch)
sha1sums=('01d065db07f6fd6bcad51811ffba69221aff656e'
          '148656352f8a20b3781ee185025c5c8d4b7d720f'
          '2b6fa7398ccb96172a99f4d12990bbc0491e355c'
          'f3c97cc9835643d48058682506eac98d6deceeb7'
          '2138342ae9f253bae003e96e91fdd0bc9918291a')

prepare()
{
  cd "$srcdir/compiz-$pkgver"

  patch -p0 -i ../compiz-gcc-4.7.patch
  patch -p1 -i ../compiz-kde-4.8.patch
  patch -p1 -i ../compiz-kde-4.9.patch
  patch -p0 -i ../compiz-kde-4.10.patch
}

build()
{
  cd "$srcdir/compiz-$pkgver"

  ./configure   --prefix=/usr \
	    --enable-gnome \
	    --enable-gtk \
	    --enable-metacity \
	    --enable-gconf \
            --with-gconf-schema-file-dir=/etc/gconf/schemas \
	    --enable-dbus \
	    --enable-librsvg \
	    --disable-kde \
	    --enable-kde4

  make QDBUSXML2CPP=/usr/bin/qdbusxml2cpp-qt4
}

package_compiz-core()
{

  pkgdesc="Composite manager for Aiglx and Xgl"
  depends=('startup-notification' 'librsvg' \
         'libgl' 'dbus' 'glu' 'libxslt' 'fuse')

  cd "$srcdir/compiz-$pkgver"
  make DESTDIR="$pkgdir" install

  # remove stuff which is in one of the decorations
   rm -rf "$pkgdir"/usr/bin/{gtk-window-decorator,kde4-window-decorator}
   rm -rf "${pkgdir}"/usr/share/{applications,gnome,gnome-control-center}
   rm -rf "${pkgdir}"/usr/lib/{window-manager-settings,pkgconfig/compiz-gconf.pc,compiz/libgconf*}
   rm -rf "${pkgdir}"/etc

  # install MIT license
  install -Dm644 "$srcdir/compiz-$pkgver/COPYING.MIT" \
	"$pkgdir/usr/share/licenses/compiz-core/COPYING.MIT"
}

package_compiz-decorator-gtk()
{
  pkgdesc="Compiz decorator for GNOME"
  depends=('gnome-control-center' 'gconf' 'libwnck' 'compiz-core' 'metacity')
  install=compiz-decorator-gtk.install

  cd "$srcdir/compiz-$pkgver"

  pushd gtk
  make DESTDIR="$pkgdir" install
  install -Dm644 window-decorator/gwd.schemas \
                "$pkgdir/usr/share/gconf/schemas/gwd.schemas"
  popd

  for i in dbus gconf ini inotify png regex svg glib kconfig
  do
    rm "$srcdir"/compiz-$pkgver/metadata/compiz-$i.schemas
  done

  gconf-merge-schema \
    "${pkgdir}"/usr/share/gconf/schemas/compiz-decorator-gtk.schemas \
        "${srcdir}"/compiz-$pkgver/metadata/*.schemas

  make DESTDIR="$pkgdir" install

  # remove stuff which is in one of the other packages
   rm -rf "${pkgdir}"/etc
   rm -rf "$pkgdir"/usr/bin/{compiz,kde4-window-decorator}
   rm -rf "$pkgdir"/usr/include
   cd "$pkgdir"/usr/lib
   rm libdecoration*
   rm pkgconfig/{compiz-cube.pc,compiz-scale.pc,compiz.pc,libdecoration.pc}
   cd "${pkgdir}"/usr/lib/compiz
   rm $(ls --hide=*gconf*)
   rm -rf "${pkgdir}"/usr/share/{compiz,locale}
   
  # install MIT license
  install -Dm644 "$srcdir"/compiz-$pkgver/COPYING.MIT \
        "$pkgdir"/usr/share/licenses/compiz-decorator-gtk/COPYING.MIT
}

package_compiz-decorator-kde()
{
  pkgdesc="Compiz decorator for KDE"
  depends=('kdebase-workspace' 'compiz-core')

  cd "$srcdir"/compiz-$pkgver
  make DESTDIR="$pkgdir" install

  # Let's remove stuff that are in compiz-core package or gtk decorator
  rm -rf "$pkgdir"/usr/{include,lib}
  rm -rf "$pkgdir"/usr/bin/{compiz,gtk-window-decorator}
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/share/{applications,gnome,gnome-control-center,locale,compiz}

  # install MIT license
  install -Dm644 "$srcdir"/compiz-$pkgver/COPYING.MIT \
        "$pkgdir"/usr/share/licenses/compiz-decorator-kde/COPYING.MIT
}

